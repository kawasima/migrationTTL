;;; migration script executor
show 0

getdir macrodir
makepath saycmd macrodir 'bin\say'

;;; select script file
filenamebox 'migration script' 0
if result=0 goto abend
cmdpath=inputstr

;;;
;;; statusbox
;;;
setdlgpos 800 50
statusbox '作業を開始します' 'README'
SAY_message = '作業を開始します'
call say
fileopen fh cmdpath 0
pause 3
closesbox

;;
gettime timestr "%Y%m%d-%H%M%S"
sprintf2 logfile 'migration_%s.log' timestr
logopen logfile 0 1
logwrite 'Start Migration'#13#10

;; fetch command
:loop
filereadln fh cmd
if result goto success

;; skip empty line
strcompare cmd ''
if result=0 goto loop
strcopy cmd 0 1 cmd_1stchar

;; read comment
strcompare cmd_1stchar '#'
if result=0 then
strlen cmd
strcopy cmd 2 result-1 ikou_comment
strconcat ikou_comment #13
strconcat disp_ikou_comment ikou_comment
goto loop
endif
;;; password line
strcopy cmd 0 5 cmd_passwd
strcompare cmd_passwd '*****'
if result=0 then
  passwordbox 'Password:' 'input password'
  cmd = inputstr
endif

;;; command
SAY_message = disp_ikou_comment
call say
strconcat disp_ikou_comment #13"=COMMAND="#13
strconcat disp_ikou_comment cmd

messagebox disp_ikou_comment 'Next command'
disp_ikou_comment = ''
send cmd

SAY_message = "実行しちゃいますよ"
call say
yesnobox '実行しちゃいますよ' '指さし確認'
if result then
  sendln ''
else
  goto abend
endif
goto loop
;;;
;;; abend
;;;
:abend
fileclose fh
logwrite 'Abort Migration'
logclose
messagebox '中止しました' 'Warning'
closesbox
end

;;;
;;; success
;;;
:success
fileclose fh
;; 時間情報取得
sendln 'date "+%Y/%m/%d %H:%M:%S"'
logwrite 'Abort Migration'
logclose
messagebox '作業完了しました' 'README'
pause 3
closesbox
end


:say
sprintf 'cmd /c %s "%s"' saycmd SAY_message
;; 音声機能を使わない場合は、以下のexec行をコメントアウトしてください。
exec inputstr 'hide'
return

